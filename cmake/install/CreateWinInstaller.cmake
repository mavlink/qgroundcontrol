message(STATUS "QGC: Creating Windows NSIS Installer")

file(TO_NATIVE_PATH "${QGC_WINDOWS_ICON_PATH}" QGC_INSTALLER_ICON)
file(TO_NATIVE_PATH "${QGC_WINDOWS_INSTALL_HEADER_PATH}" QGC_INSTALLER_HEADER_BITMAP)
file(TO_NATIVE_PATH "${QGC_WINDOWS_DRIVER_MSI}" QGC_INSTALLER_DRIVER_MSI)
file(TO_NATIVE_PATH "${QGC_WINDOWS_INSTALLER_SCRIPT}" QGC_NSIS_INSTALLER_SCRIPT)
file(TO_NATIVE_PATH "${QGC_WINDOWS_OUT}" QGC_INSTALLER_OUT)

set(QGC_NSIS_INSTALLER_PARAMETERS
    /DDRIVER_MSI=${QGC_INSTALLER_DRIVER_MSI}
    /DINSTALLER_ICON=${QGC_INSTALLER_ICON}
    /DHEADER_BITMAP=${QGC_INSTALLER_HEADER_BITMAP}
    /DAPPNAME=${CMAKE_PROJECT_NAME}
    /DEXENAME=${CMAKE_PROJECT_NAME}
    /DORGNAME=${QGC_ORG_NAME}
    /DDESTDIR=${CMAKE_INSTALL_PREFIX}
    /NOCD
    "/XOutFile ${QGC_INSTALLER_OUT}"
    ${QGC_NSIS_INSTALLER_SCRIPT}
)

set(_pf86 "ProgramFiles(x86)")
set(_PF86 "PROGRAMFILES(x86)")
find_program(QGC_NSIS_INSTALLER_CMD makensis
    PATHS "$ENV{Programfiles}" "$ENV{PROGRAMFILES}" "$ENV{${_pf86}}" "$ENV{${_PF86}}" "$ENV{ProgramW6432}" "$ENV{PROGRAMW6432}"
    PATH_SUFFIXES "NSIS"
    DOC "Path to the makensis utility."
    REQUIRED
)

execute_process(
    COMMAND ${QGC_NSIS_INSTALLER_CMD} ${QGC_NSIS_INSTALLER_PARAMETERS}
    COMMAND_ECHO STDOUT
    COMMAND_ERROR_IS_FATAL ANY
)
