function(qgc_set_qt_resource_alias)
    foreach(resource_file IN LISTS ARGN)
        get_filename_component(alias "${resource_file}" NAME)
        set_source_files_properties("${resource_file}"
            PROPERTIES
                QT_RESOURCE_ALIAS "${alias}"
        )
    endforeach()
endfunction()

function(qgc_config_caching)
    function(_qgc_verify_cache_tool _ok _path)
        execute_process(
            COMMAND "${_path}" --version
            RESULT_VARIABLE _res
            OUTPUT_QUIET ERROR_QUIET
        )
        if(NOT _res EQUAL 0)
            set(${_ok} FALSE PARENT_SCOPE)
        endif()
    endfunction()

    find_program(QGC_CACHE_PROGRAM
                 NAMES ccache sccache
                 VALIDATOR _qgc_verify_cache_tool)
    if(QGC_CACHE_PROGRAM)
        get_filename_component(_cache_tool "${QGC_CACHE_PROGRAM}" NAME_WE)
        message(STATUS "QGC: using ${_cache_tool} (${QGC_CACHE_PROGRAM})")
        string(TOLOWER "${_cache_tool}" _cache_tool)

        if(_cache_tool STREQUAL "ccache")
            # set(ENV{CCACHE_CONFIGPATH} "${CMAKE_SOURCE_DIR}/tools/ccache.conf")
            # set(ENV{CCACHE_DIR} "${CMAKE_SOURCE_DIR}/.ccache")
            set(ENV{CCACHE_BASEDIR} "${CMAKE_SOURCE_DIR}")
            set(ENV{CCACHE_COMPRESSLEVEL "5")
            set(ENV{CCACHE_SLOPPINESS} "pch_defines,time_macros,include_file_mtime,include_file_ctime")
            # set(ENV{CCACHE_NOHASHDIR} "true")
            set(ENV{CCACHE_COMPILERCHECK} "content")
            # set(ENV{CCACHE_MAXSIZE} "1G")
        elseif(_cache_tool STREQUAL "sccache")
            # set(ENV{SCCACHE_PATH} "")
            # set(ENV{SCCACHE_DIR} "")
            # set(ENV{SCCACHE_SERVER_PORT} "")
            # set(ENV{SCCACHE_SERVER_UDS} "")
            # set(ENV{SCCACHE_IGNORE_SERVER_IO_ERROR} "")
            # set(ENV{SCCACHE_C_CUSTOM_CACHE_BUSTER} "")
            # set(ENV{SCCACHE_RECACHE} "")
            # set(ENV{SCCACHE_ERROR_LOG} "")
            # set(ENV{SCCACHE_LOG} "")
            # set(ENV{SCCACHE_CACHE_SIZE} "")
            # set(ENV{SCCACHE_IDLE_TIMEOUT} "")
        else()
            return()
        endif()

        set(CMAKE_C_COMPILER_LAUNCHER "${QGC_CACHE_PROGRAM}" CACHE STRING "C compiler launcher")
        set(CMAKE_CXX_COMPILER_LAUNCHER "${QGC_CACHE_PROGRAM}" CACHE STRING "CXX compiler launcher")
        # set(CMAKE_C_LINKER_LAUNCHER "${QGC_CACHE_PROGRAM}" CACHE STRING "C linker cache used")
        # set(CMAKE_CXX_LINKER_LAUNCHER "${QGC_CACHE_PROGRAM}" CACHE STRING "CXX linker cache used")

        if(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
            add_compile_options(-Xclang -fno-pch-timestamp)
        endif()
    else()
        message(WARNING "QGC: no ccache/sccache found – building without a compiler cache")
    endif()
endfunction()

function(qgc_set_linker)
    include(CheckLinkerFlag)

    foreach(_ld mold lld gold)
        set(_flag "LINKER:-fuse-ld=${_ld}")
        check_linker_flag(CXX "${_flag}" HAVE_LD_${_ld})

        if(HAVE_LD_${_ld})
            add_link_options("${_flag}")
            set(QGC_LINKER "${_ld}")
            message(STATUS "QGC: using ${_ld} linker (flag ${_flag})")
            break()
        endif()
    endforeach()

    if(NOT DEFINED QGC_LINKER)
        message(WARNING "QGC: no mold / lld / gold found – falling back to default linker")
    endif()
endfunction()

function(qgc_enable_pie)
    include(CheckPIESupported)
    check_pie_supported(OUTPUT_VARIABLE _output)
    if(CMAKE_C_LINK_PIE_SUPPORTED)
        set(CMAKE_POSITION_INDEPENDENT_CODE ON)
        message(STATUS "QGC: PIE is enabled")
    else()
        message(WARNING "QGC: PIE is not supported at link time: ${_output}")
    endif()
endfunction()

function(qgc_enable_ipo)
    if(CMAKE_BUILD_TYPE STREQUAL "Release")
        include(CheckIPOSupported)
        check_ipo_supported(RESULT _result OUTPUT _output)
        if(_result)
            set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)
            message(STATUS "QGC: LTO is enabled")
        else()
            message(WARNING "QGC: IPO is not supported: ${_output}")
        endif()
    endif()
endfunction()
