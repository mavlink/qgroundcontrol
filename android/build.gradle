buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.8.0'
    }
}

repositories {
    google()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

if (project.plugins.findPlugin("qtGradlePluginType") != null) {
    apply plugin: qtGradlePluginType
} else {
    // Fall back to the default Android application plugin if qtGradlePluginType
    // is not available
    apply plugin: 'com.android.application'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    //noinspection GradleDependency
    implementation 'androidx.core:core:1.13.1'
    implementation 'com.github.mik3y:usb-serial-for-android:3.8.1'
}

def getTimestampStr() { return new Date().format('yyyyMMddHHmmss') }
def timestampInt = (int)(new Date().getTime() / 1000 / 1)

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qtAndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     * - qtGradlePluginType - whether to build an app or a library
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    // namespace = androidPackageName
    namespace = "org.mavlink.qgroundcontrol"
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion
    ndkVersion = androidNdkVersion

    // Extract native libraries from the APK
    packagingOptions.jniLibs.useLegacyPackaging = true

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qtAndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qtAndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qtAndroidDir + '/res', 'res']
            resources.srcDirs = ['resources']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }
    }

    tasks.withType(JavaCompile) {
        options.incremental = true
        // options.compilerArgs += ['-Xlint:deprecation']
    }

    buildFeatures {
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    lintOptions {
        abortOnError = false
    }

    // Do not compress Qt binary resources file
    aaptOptions {
        noCompress 'rcc'
    }

    defaultConfig {
        resConfig "en"
        minSdkVersion qtMinSdkVersion
        targetSdkVersion qtTargetSdkVersion
        ndk.abiFilters = qtTargetAbiList.split(",")

        // println "applicationId: ${applicationId}"
        println "minSdkVersion: ${minSdkVersion}"
        println "targetSdkVersion: ${targetSdkVersion}"
        println "ndk.abiFilters: ${ndk.abiFilters}"
        println "versionCode: ${versionCode}"
        println "versionName: ${versionName}"
    }

    println "timestamp: " + getTimestampStr()
    println "androidPackageName: ${namespace}"
    println "androidCompileSdkVersion: ${compileSdkVersion}"
    println "androidBuildToolsVersion: ${buildToolsVersion}"
    println "androidNdkVersion: ${ndkVersion}"
    println "project.name: ${project.name}"
    println "qtAndroidDir: ${qtAndroidDir}"
}
